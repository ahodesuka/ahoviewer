_realname=unrar
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=6.1.4
pkgrel=1
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64')
url="https://www.rarlab.com/rar_add.htm"
license=("custom")
options=('strip' 'staticlibs')
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs")
source=("https://www.rarlab.com/rar/unrarsrc-${pkgver}.tar.gz"
        'build-dll.patch')
sha256sums=('c0ed58629243961c3f1ec90c08b11ff93261e568dbfdce2bf3b759ee7a4a3b7c'
            'c5d04090129df2e06b376a11a62acf8948bedf0033c95d3287aff6e3e980ca91')

prepare() {
  cd ${_realname}

  patch -p1 -i ${srcdir}/build-dll.patch
}

build() {
  cd ${_realname}
  LIBS="${MINGW_PREFIX}/lib/libpowrprof.a" make lib CXXFLAGS="${CXXFLAGS}"
}

package() {
  cd "${srcdir}/${_realname}"

  install -Dm755 libunrar.dll "${pkgdir}${MINGW_PREFIX}/bin/libunrar.dll"
  install -Dm755 libunrar.dll.a "${pkgdir}${MINGW_PREFIX}/lib/libunrar.dll.a"
  install -Dm644 dll.hpp "${pkgdir}${MINGW_PREFIX}/include/unrar/dll.hpp"
  install -Dm644 ${srcdir}/${_realname}/license.txt "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}